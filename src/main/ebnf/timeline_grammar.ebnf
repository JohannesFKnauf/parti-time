(* A timeline represents one or multiple consecutive days. *)
timeline = day ( <EOL> day )*

(* A day block starts with a mandatory isodate as explicit frame of reference. *)
(* The date may be followed by one or more time entries. *)
day = reference-date entries

reference-date = isodate <EOL>

entries = entry+

(* An entry consists of a mandatory header and optional details. *)
entry = entry-header activities?

(* A entry header is given by the entry's start time and its project *)
<entry-header> = hhmm-time <" "> project <EOL>
project = rest-of-line

activities = activity+
activity =  <no-time " "> rest-of-line <EOL>

<isodate> = #"\p{Digit}{4}-\p{Digit}{2}-\p{Digit}{2}"
hhmm-time = #"\p{Digit}{4}"
no-time = #" {4}"

<rest-of-line> = #"[^\r\n]*"

(* EOF is considered equivalent to EOL. *)
EOS = EOL | EOF
EOL = #"\r?\n"
EOF = #"\z"
