(* A timeline starts with a mandatory isodate providing the inital frame of reference. *)
(* A timeline consists of a series of entries. *)
timeline = &isodate ( entry <EOL?> )*

(* An entry starts with an optional isodate as explicit frame of reference. A missing date will be interpreted as "next best date for the timestamp at hand". *)
(* The start-on-project is mandatory and can not be ommitted. *)
(* Entries may be annotated by an arbitrary number of sub-items. *)
entry = reference-date? time-partition-start details?

details = detail+

reference-date = isodate <EOL>
<time-partition-start> = hhmm-time <" "> subject <EOS>
detail =  <no-time " "> rest-of-line <EOS>

subject = rest-of-line

<isodate> = #"\p{Digit}{4}-\p{Digit}{2}-\p{Digit}{2}"
hhmm-time = #"\p{Digit}{4}"
no-time = #" {4}"

<rest-of-line> = #"[^\r\n]*"

(* EOF is considered equivalent to EOL. *)
EOS = EOL | EOF
EOL = #"\r?\n"
EOF = #"\z"
